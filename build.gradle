plugins {
    id "io.freefair.lombok" version "6.4.0"
    id 'war'
    id 'signing'
    id 'maven-publish'
}



version = '1.0.2'

group = 'nu.mine.mosher.servlet'

repositories {
    mavenCentral()
    maven {
        url = uri('https://public:\u0067hp_fya6Kseu3XOBMg2icbg7f1LP6ZFYjj35v4Zj@maven.pkg.github.com/cmosher01/*')
    }
}

dependencies {
    providedCompile group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: 'latest.release'

    implementation group: 'org.apache.tika', name: 'tika-core', version: 'latest.release'
    implementation group: 'org.apache.tika', name: 'tika-parsers-standard-package', version: 'latest.release', {
        exclude group: 'xml-apis'
    }

    implementation group: 'org.jsoup', name: 'jsoup', version: 'latest.release'

    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: 'latest.release'
    runtimeOnly group: 'com.fasterxml.woodstox', name: 'woodstox-core', version: 'latest.release'

    runtimeOnly group: 'xerces', name: 'xercesImpl', version: 'latest.release', {
        exclude group: 'xml-apis'
    }
    runtimeOnly group: 'net.sf.saxon', name: 'Saxon-HE', version: 'latest.release'

    runtimeOnly group: 'nu.mine.mosher.xml', name: 'tei-to-xhtml5', version: 'latest.integration'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: 'latest.integration'
    runtimeOnly group: 'nu.mine.mosher.io', name: 'adapt-slf4j-to-servlet', version: 'latest.release'
}



java {
    toolchain {
        languageVersion = JavaLanguageVersion.of 17
    }
}



publishing {
    publications {
        maven(MavenPublication) {
            from components.web
            pom {
                name = 'xml-servlet'
                description = 'Servlet for XML-related documents'
                url = 'https://github.com/cmosher01/xml-servlet'
                inceptionYear = '2021'
                packaging = 'war'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'cmosher01'
                        name = 'Christopher Alan Mosher'
                        email = 'cmosher01@gmail.com'
                        url = 'https://mosher.mine.nu'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cmosher01/xml-servlet.git'
                    developerConnection = 'scm:git:ssh://github.com/cmosher01/xml-servlet.git'
                    url = 'https://github.com/cmosher01/xml-servlet'
                }
            }
        }
    }
}
tasks.assemble.dependsOn tasks.generatePomFileForMavenPublication

signing {
    sign publishing.publications.maven
}
if (hasProperty('signing.secretKeyRingFile')) {
    tasks.assemble.dependsOn tasks.signMavenPublication
}

publishing {
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty('ossrhUsername') ?: 'restricted'
                password = findProperty('ossrhPassword') ?: 'restricted'
            }
        }
    }
}
